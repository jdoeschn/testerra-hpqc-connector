plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext {
    doNotPublish = this.&dnp

    testerraVersion = '[1.0-RC,2.0)'
    moduleVersion = '1-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }
}

def dnp(prj) {
    // Add `doNotPublish(this)` to module's build.gradle if no publishing is needed
    prj.jar.enabled = true
    prj.publish.enabled = false
    prj.publishToMavenLocal.enabled = false
    println("Do not publish JAR from " + prj)
}

allprojects {
    group 'eu.tsystems.mms.tic.testerra'
    version moduleVersion
}

subprojects {

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.whenTaskAdded { t ->
        if (t.name.startsWith("publish")) {
            boolean e = t.project.publish.enabled
            if (!e) {
                println "Not running publish for " + t.project.name
                t.enabled = false
            }
        }
    }

    test {
        useTestNG()
    }
}

task publishToBintray(type: GradleBuild) {
    tasks = [
            'qc11-connector:bintrayUpload',
            'qc-restclient:bintrayUpload'
    ]
}



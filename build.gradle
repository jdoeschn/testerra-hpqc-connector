ext {
    doNotPublish = this.&dnp

    // Minimum required Testerra version
    testerraCompileVersion = '1.0.0'
    // Unit tests use the latest Testerra version
    testerraTestVersion = '[1.0,2-SNAPSHOT)'
    moduleVersion = '1-SNAPSHOT'
    if (System.properties.containsKey('moduleVersion')) {
        moduleVersion = System.getProperty('moduleVersion')
    }
}

allprojects {

    group 'io.testerra'
    version moduleVersion

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    // important!
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.whenTaskAdded { t ->
        if (t.name.startsWith("publish")) {
            boolean e = t.project.publish.enabled
            if (!e) {
                println "Not running publish for " + t.project.name
                t.enabled = false
            }
        }
    }

    test {
        useTestNG()
    }
}

def dnp(prj) {
    // Add `doNotPublish(this)` to module's build.gradle if no publishing is needed
    prj.jar.enabled = true
    prj.publish.enabled = false
    prj.publishToMavenLocal.enabled = false
    println("Do not publish JAR from " + prj)
}

// Do not move this integration because `group` and `version` is needed for publishing
apply from: rootProject.file('publish.gradle')

